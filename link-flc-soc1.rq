#+ endpoint: http://localhost:1234/sparql/

prefix obo: <http://purl.obolibrary.org/obo/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix term: <http://rdf.ebi.ac.uk/terms/ensembl/>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix faldo: <http://biohackathon.org/resource/faldo#>
prefix resource: <http://semanticscience.org/resource/>
prefix core: <http://purl.uniprot.org/core/>
prefix so: <http://purl.obolibrary.org/obo/so#>
prefix ensembl: <http://rdf.ebi.ac.uk/resource/ensembl/>
prefix TAIR10: <http://rdf.ebi.ac.uk/resource/ensembl/33/arabidopsis_thaliana/TAIR10/>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT DISTINCT (str(?regulator) AS ?candidate_gene) (str(?interaction_label) AS ?interaction_evidence) (str(?function) AS ?shared_BP_GO_term)
WHERE{

# determine the QTL interval
VALUES ?ref{ TAIR10:5 }
FILTER (?begin_pos >= 2000000 && ?end_pos <= 4000000) .

# get the list of candidate in the QTL interval
?gene a                     obo:SO_0001217 .
?gene faldo:location            ?gene_loc .
?gene_loc faldo:begin           ?begin ;
          faldo:end             ?end ;
          faldo:reference       ?ref .
          ?begin faldo:position ?begin_pos .
          ?end faldo:position   ?end_pos .

# query the candidate that interact with the target gene
?gene (resource:SIO_000701|^resource:SIO_000701) ensembl:AT2G45660 ;
      obo:RO_0000056 ?interaction .
ensembl:AT2G45660 obo:RO_0000056 ?interaction .
?interaction a ?interaction_type .
?interaction_type rdfs:label ?interaction_label .

# query the candidate that shared bp go term
?gene resource:SIO_000255 ?go1 .
ensembl:AT2G45660 resource:SIO_000255 ?go2 .

?go2 oboInOwl:hasOBONamespace "biological_process"^^<http://www.w3.org/2001/XMLSchema#string> ;
     (rdfs:subClassOf|^rdfs:subClassOf){,2} ?go1 ;
     rdfs:label ?function .

# get the gene label
?gene rdfs:label ?regulator .

}
